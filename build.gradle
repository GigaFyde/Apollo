import java.time.Clock

plugins {
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.1.0'
}
group 'dev.gigafyde'

mainClassName = 'dev.gigafyde.apollo.Main'
sourceCompatibility = 11

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }

}

dependencies {
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.3.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.3'
    implementation group: 'io.sentry', name: 'sentry-logback', version: '4.3.0'
    implementation 'com.sedmelluq:lavaplayer:1.3.77'
    implementation 'com.github.freyacodes:Lavalink-Client:3ea4125238'
    implementation("net.dv8tion:JDA:4.2.1_253") {
        exclude module: "opus-java"
    }
}

jib {
    from {
        image = 'arm64v8/openjdk:11-jre'
        platforms {
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }

    container {
        mainClass = 'dev.gigafyde.apollo.Main'
        creationTime = Clock.systemUTC().instant()
    }
}

compileJava {
    classpath = sourceSets.main.compileClasspath
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}
